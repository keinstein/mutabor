LOGIC
    Equal Key E = Semi
    [
        Key T -> Third
        Key Q -> Quarter
        Key A -> Eighth
        Key S -> Sixteenth
	Key F -> FourtyThree
    ]

    Historic Key H =
    [
  	Key P -> Pythagorean
"%"        Key M -> Mitteltoenig
        Key S -> Silbermann
        Key W -> Werckmeister
        Key K -> Kirnberger3
        Key B -> Bach
        Key C -> Schlick
    ]

    ToneNet Key N = C_Major
    [
        SHIFTED nThird -> Transpo ( DISTANCE )
   	SHIFTED nMajor -> Transpo ( DISTANCE )
        SHIFTED nMinor -> Transpo ( DISTANCE )
    ]

    OverTones Key O = OverTones [ ]

    User Key U = Init
    [
    	Key U -> Up   ( DISTANCE )
   	Key D -> Down ( DISTANCE )
	SHIFTED aKey -> Empty
    ]

INTERVAL
    SemiTone      = 12 ROOT 2
    ThirdTone     = 18 ROOT 2
    QuarterTone   = 24 ROOT 2
    EighthTone    = 48 ROOT 2
    SixteenthTone = 96 ROOT 2
    FourtyThird   = 43 ROOT 2

    Third      = 5:4
    Fifth      = 3:2
    Octave     = 2:1
    NatSeventh = 7:4

    Cent = 1200 ROOT 2

" ==== Equal-tempered Tunings ======================================= "

TONESYSTEM
    Semi        = 60 [ c ] SemiTone
    Third       = 60 [ c ] ThirdTone
    Quarter     = 60 [ c ] QuarterTone
    Eighth      = 60 [ c ] EighthTone
    Sixteenth   = 60 [ c ] SixteenthTone
    FourtyThree = 60 [ c ] FourtyThird

TONE "common tone declaration"
    a = 440
    c = a-Third+Fifth-Octave

" ==== Historic Tunings ============================================= "

  Quellen: - B.Billeter, Anweisung zum Stimmen von Tasteninstrumenten
           - H.Kelletat, Zur musikalischen Temperatur (Band I + II)
           - Wandel&Goltermann (Hrsg.), Ein Beitrag zur musik.Temp.
                                der Musikinstrumente vom Mittelalter
                                bis zur Gegenwart

  ==== Pythagorean Tone System ====================================== "

TONE
    pc = c
    pd = pg + Fifth - Octave
    pe = pa + Fifth - Octave
    pf = pc - Fifth + Octave
    pg = pc + Fifth
    pa = pd + Fifth
    ph = pe + Fifth

TONSYSTEM
    Pythagorean = 60 [ pc, ,pd, ,pe,pf, ,pg, ,pa, ,ph] Octave


" ==== Mitteltoenig ================================================= "

INTERVAL
    SynKomma=81:80

TONE
    mc   = c
    mcis = mfis + Fifth - 0.25 Synkomma - Octave
    md   = mg   + Fifth - 0.25 Synkomma - Octave
    mes  = mb   - Fifth + 0.25 Synkomma
    me   = ma   + Fifth - 0.25 Synkomma - Octave
    mf   = mc   - Fifth + 0.25 Synkomma + Octave
    mfis = mh   + Fifth - 0.25 SynKomma - Octave
    mg   = mc   + Fifth - 0.25 Synkomma
    mgis = mcis + Fifth - 0.25 Synkomma
    ma   = md   + Fifth - 0.25 Synkomma
    mb   = mf   - Fifth + 0.25 Synkomma + Octave
    mh   = me   + Fifth - 0.25 Synkomma

TONESYSTEM
    Mitteltoenig = 60 [ mc,mcis,md,mes,me,mf,mfis,mg,mgis,ma,mb,mh] Octave


" ==== Silbermann =================================================== "

TONE
    sc   = c
    scis = sfis + Fifth - 0.181 Synkomma - Octave
    sd   = sg   + Fifth - 0.181 Synkomma - Octave
    ses  = sb   - Fifth + 0.181 Synkomma
    se   = sa   + Fifth - 0.181 Synkomma - Octave
    sf   = sc   - Fifth + 0.181 Synkomma + Octave
    sfis = sh   + Fifth - 0.181 SynKomma - Octave
    sg   = sc   + Fifth - 0.181 Synkomma
    sgis = scis + Fifth - 0.181 Synkomma
    sa   = sd   + Fifth - 0.181 Synkomma
    sb   = sf   - Fifth + 0.181 Synkomma + Octave
    sh   = se   + Fifth - 0.181 Synkomma

TONESYSTEM
    Silbermann = 60 [ sc,scis,sd,ses,se,sf,sfis,sg,sgis,sa,sb,sh] Octave


" ==== Werckmeister =================================================

  Bei vielen Stimmungen wurde eine Inkonsistenz in der Literatur
  bemerkt. Wenn dort in der Stimmanweisung die Rede war von
  'Die Quinten von C bis A mitteltönig', und wir die mitteltönige
  Fifth mit 1/4 Synton.Komma ansetzen, so erhält man bereits beim
  A Abweichungen von über 1.4 Cent gegenüber den Centtabellen in
  derselbel Literatur! Deshalb habe ich hier ein wenig interpoliert. "

INTERVAL
    MQ = 1.495:1 "a little bit lower than the exactly calulated fifth"

TONE
    wc   = c
    wcis = wgis - Fifth
    wd   = wg + MQ - Octave
    wdis = wb - Fifth
    we   = wa + Fifth - Octave
    wf   = wc - Fifth + Octave
    wfis = wcis - Fifth + Octave
    wg   = wc + MQ
    wgis = wdis - Fifth + Octave
    wa   = wd + MQ
    wb   = wf - Fifth + Octave
    wh   = we + Fifth

TONESYSTEM
    Werckmeister = 60 [wc,wcis,wd,wdis,we,wf,wfis,wg,wgis,wa,wb,wh] Octave


" ==== Kirnberger 3 ================================================= "
" Das pyth. Komma wird ungleichm„áig auf die
  Quinten C-G-D-A und Cis-Fis verteilt.
  siehe H.Kelletat, Zur musik. Temp. Band II, 1982, S.140 "

INTERVAL
    p1_12   = 1.00112989063:1 "1/12   pyth. Komma"
    p2_12   = 1.00226105791:1 "2/12   pyth. Komma"
    p25_120 = 1.00282712075:1 "2,5/12 pyth. Komma"
    p3_12   = 1.00339350329:1 "3/12   pyth. Komma"
    p35_120 = 1.00396020572:1 "3,5/12 pyth. Komma"
    PythKomma = 531441:524288

TONE
    kc   = c
    kg   = kc + Fifth- p2_12
    kd   = kg + Fifth- p25_120 - Octave
    ka   = kd + Fifth - p35_120
    ke   = ka + Fifth - p3_12 - Octave
    kcis = kfis + Fifth - p1_12 - Octave
    kh   = ke + Fifth
    kb   = kf - Fifth + Octave
    kes  = kb - Fifth
    kf   = kc - Fifth + Octave
    kgis = kcis + Fifth
    kfis = kh + Fifth - Octave

TONESYSTEM
    Kirnberger3 = 60 [kc,kcis,kd,kes,ke,kf,kfis,kg,kgis,ka,kb,kh] Octave


" ==== 'Bach' (nach Billeter 1979) ================================== "

  hier erschien es mir am sinnvollsten, einfach die in der
  Literatur angegebene Cent-tabelle abzuschreiben.              "

TONE
    bc   = c
    bcis = bc +   92 cent
    bd   = bc +  200 cent
    bes  = bc +  296 cent
    be   = bc +  390 cent
    bf   = bc +  500 cent
    bfis = bc +  590 cent
    bg   = bc +  700 cent
    bgis = bc +  794 cent
    ba   = bc +  895 cent
    bb   = bc +  998 cent
    bh   = bc + 1090 cent

TONESYSTEM
   Bach = 60 [bc,bcis,bd,bes,be,bf,bfis,bg,bgis,ba,bb,bh] Octave


" ==== Schlick ======================================================

  Es hat über eine Stunde gebraucht, bis ich diese Stimmung fertig
  programmiert hatte. Dies lag an der schwer zu interpretierenden
  Literatur über die Schlick-Stimmung. Angaben im Buch stimmten nicht
  mit dem Hörtest überein, und so habe ich mehrmals neu programmiert.
  ich hoffe, die Stimmung nun getroffen zu haben.                     "

INTERVAL
    Spy = 1.002261058:1 "sixth of a Pythagorean comma"
    Zpy = 1.001129891:1 "twelfth of a Pythagorean comma"

TONE
    cf  = c - Fifth + Spy + Octave
    cg  = c + Fifth-Spy
    cd  = cg + Fifth - Spy - Octave
    ca  = cd + Fifth - Spy
    ce  = ca + Fifth - Spy - Octave
    ch  = ce + Fifth - Spy
    cb  = cf - Fifth + Zpy + Octave
    ces = cb - Fifth + Zpy
    cas = ces - 708cent + Octave
    cfis= ch + Fifth - Zpy - Octave
    ccis= cfis + Fifth - Zpy - Octave

TONESYSTEM
    Schlick = 60 [c,ccis,cd,ces,ce,cf,cfis,cg,cas,ca,cb,ch] Octave


" ==== Tonale Net ===================================================

  Eine mutierende Rein-Stimmung nach Martin Vogel. "

TONE
    nc   = nf + Fifth - Octave
    ndes = nas - Fifth
    nd   = ng + Fifth - Octave
    nes  = ng-Third
    ne   = nc+Third
    nf   = na-Third
    nfis = nd+Third
    ng   = nc+Fifth
    nas  = nc-Third + Octave
    na   = 440
    nb   = nf-Fifth + Octave
    nh   = ng+Third

TONESYSTEM
    C_dur = 60  [ nc,ndes,nd,nes,ne,nf,nfis,ng,nas,na,nb,nh] Octave

RETUNING
    Transpo(x) = @+x[]

PATTERN
    nThird = {0,4,*7,*10}
    nMajor = {0,*4,7}
    nMinor = {0,3,7}

" ==== Overtones ====================================================
  At whithe keys there is an overtone sequenz, at the black ones an
  undertone sequenz; uses 3 octaves of the piano "

INTERVAL
    i1=1:1   i2=2:1   i3=3:1   i4=4:1   i5=5:1   i6=6:1
    i7=7:1   i8=8:1   i9=9:1   i10=10:1 i11=11:1 i12=12:1
    i13=13:1 i14=14:1 i15=15:1 i16=16:1 i17=17:1 i18=18:1
    i19=19:1 i20=20:1 i21=21:1 i22=22:1 i23=23:1 i24=24:1
    i25=25:1 i27=27:1 i28=28:1 i30=30:1

TONE
    AA=110
    o2=AA+i2   o3=AA+i3   o4=AA+i4   o5=AA+i5   o6=AA+i6   o7=AA+i7
    o8=AA+i8   o9=AA+i9   o10=AA+i10 o11=AA+i11 o12=AA+i12 o13=AA+i13
    o14=AA+i14 o15=AA+i15 o16=AA+i16 o17=AA+i17 o18=AA+i18 o19=AA+i19
    o20=AA+i20 o21=AA+i21 o22=AA+i22 o23=AA+i23 o24=AA+i24 o25=AA+i25
    o27=AA+i27 o28=AA+i28 o30=AA+i30
    CC=AA+5i2
    u2=CC-i2   u3=CC-i3   u4=CC-i4   u5=CC-i5   u6=CC-i6   u7=CC-i7
    u8=CC-i8   u9=CC-i9   u10=CC-i10 u11=CC-i11 u12=CC-i12 u13=CC-i13
    u14=CC-i14 u15=CC-i15 u16=CC-i16 u17=CC-i17 u18=CC-i18 u19=CC-i19
    u20=CC-i20 u21=CC-i21 u22=CC-i22 u23=CC-i23 u24=CC-i24

TONESYSTEM
    Obertoene = 60 [ AA,u15,o2,u14,o3,o4,u13,o5,u12,o6,u11,o7,
                  o8,u10,o9,u9,o10,o11,u8,o12,u7,o13,u6,o14,
                  o15,u5,o16,u4,o17,o18,u3,o19,u2,o20,CC,o21 ] i1

" ==== User =========================================================
  The last key that was hit on the piano can be changed by computer
  keyboard."

RETUNING
    U__0 = [ @+Delta, @, @, @, @, @, @, @, @, @, @, @]
    U__1 = [ @, @+Delta, @, @, @, @, @, @, @, @, @, @]
    U__2 = [ @, @, @+Delta, @, @, @, @, @, @, @, @, @]
    U__3 = [ @, @, @, @+Delta, @, @, @, @, @, @, @, @]
    U__4 = [ @, @, @, @, @+Delta, @, @, @, @, @, @, @]
    U__5 = [ @, @, @, @, @, @+Delta, @, @, @, @, @, @]
    U__6 = [ @, @, @, @, @, @, @+Delta, @, @, @, @, @]
    U__7 = [ @, @, @, @, @, @, @, @+Delta, @, @, @, @]
    U__8 = [ @, @, @, @, @, @, @, @, @+Delta, @, @, @]
    U__9 = [ @, @, @, @, @, @, @, @, @, @+Delta, @, @]
    U_10 = [ @, @, @, @, @, @, @, @, @, @, @+Delta, @]
    U_11 = [ @, @, @, @, @, @, @, @, @, @, @, @+Delta]

    Up(dist) = dist
    {
        0 -> U__0
     	1 -> U__1
     	2 -> U__2
     	3 -> U__3
     	4 -> U__4
     	5 -> U__5
     	6 -> U__6
     	7 -> U__7
     	8 -> U__8
    	9 -> U__9
    	10 -> U_10
    	11 -> U_11
    }

    D__0 = [ @-Delta, @, @, @, @, @, @, @, @, @, @, @]
    D__1 = [ @, @-Delta, @, @, @, @, @, @, @, @, @, @]
    D__2 = [ @, @, @-Delta, @, @, @, @, @, @, @, @, @]
    D__3 = [ @, @, @, @-Delta, @, @, @, @, @, @, @, @]
    D__4 = [ @, @, @, @, @-Delta, @, @, @, @, @, @, @]
    D__5 = [ @, @, @, @, @, @-Delta, @, @, @, @, @, @]
    D__6 = [ @, @, @, @, @, @, @-Delta, @, @, @, @, @]
    D__7 = [ @, @, @, @, @, @, @, @-Delta, @, @, @, @]
    D__8 = [ @, @, @, @, @, @, @, @, @-Delta, @, @, @]
    D__9 = [ @, @, @, @, @, @, @, @, @, @-Delta, @, @]
    D_10 = [ @, @, @, @, @, @, @, @, @, @, @-Delta, @]
    D_11 = [ @, @, @, @, @, @, @, @, @, @, @, @-Delta]

    Down(dist) = dist
    {
   	0 -> D__0
     	1 -> D__1
     	2 -> D__2
     	3 -> D__3
     	4 -> D__4
     	5 -> D__5
     	6 -> D__6
     	7 -> D__7
     	8 -> D__8
     	9 -> D__9
    	10 -> D_10
    	11 -> D_11
    }

    Init = [<< 12 >>]
    Empty = @ + 0 [ ]

INTERVAL
    Delta = 120 ROOT 2

PATTERN
    aKey = { 0, *1, *2, *3, *4, *5, *6, *7, *8, *9, *10, *11 }


" ==== MIDI channels ================================================ "

MIDICHANNEL
    1 -> 1 - 16
    2 -> 1 - 16 [1] 	
    3 -> 1 - 16 [1] 	
    4 -> 1 - 16 [1]
    5 -> 1 - 16 [1]
    6 -> 1 - 16 [1]
    7 -> 1 - 16 [1]
    8 -> 1 - 16 [1]
    9 -> 1 - 16 [1]
   10 -> 1 - 16 [1]
   11 -> 1 - 16 [1]
   12 -> 1 - 16 [1]
   13 -> 1 - 16 [1]
   14 -> 1 - 16 [1]
   15 -> 1 - 16 [1]		
   16 -> 1 - 16 [1]	

