"     Tuning logics for test phase                           "

LOGIK
  C_maj TASTE C = init_C_maj [
  
   taste Q  -> gotoCmajI   
   taste W  -> gotoCmajii  
   taste E  -> gotoCmajiii 
   taste R  -> gotoCmajIV  
   taste T  -> gotoCmajV   
   taste Z  -> gotoCmajVI  
   taste U  -> gotoCmajVII 
   taste I  -> gotoCmajV7  

   taste Y  -> decreaseB
   taste X  -> increaseB
                                         ]
  A_min TASTE A = init_A_min [ ]

"     In- or decreasing of the B                             "

UMSTIMMUNG
  decreaseB = [ @,@,@,@,@,@,@,@,@,@,@,@ - 1 cent ]
  increaseB = [ @,@,@,@,@,@,@,@,@,@,@,@ + 1 cent ]
 
"     Retuning instructions GOTO...                          "

UMSTIMMUNG
  gotoCmajI    = { init_C_maj , majI }
  gotoCmajii   = { init_C_maj , majii }
  gotoCmajiii  = { init_C_maj , majiii }
  gotoCmajIV   = { init_C_maj , majIV }
  gotoCmajV    = { init_C_maj , majV }
  gotoCmajvi   = { init_C_maj , majvi }
  gotoCmajvii  = { init_C_maj , majvii }
  gotoCmajV7   = { init_C_maj , majV7 }



"     Initial tonescales                                     "

TONSYSTEM
  init_C_maj = 60 [ majC0, majC1, majC2, majC3, majC4, majC5, majC6,
                    majC7, majC8, majC9, majC10, majC11 ] octave
  init_A_min = 57 [ minA0, minA1, minA2, minA3, minA4, minA5, minA6,
                    minA7, minA8, minA9, minA10, minA11 ] octave


"     Retunings (major)                                      "

UMSTIMMUNG
  majI    = [ @,@,@,@,@+majCI4,@,@,@,@,@,@,@ ]
  majii   = [ @ ] "nothing 's changed"
  majiii  = [ @,@,@,@,@,@,@,@,@,@,@,@+majCiii11 ]
  majIV   = [ @,@,@,@,@,@,@,@,@,@+majCIV9 ]
  majV    = [ @,@,@+majCV2,@,@,@,@,@,@,@,@,@+majCV11 ]
  majvi   = [ @ ] "nothing 's changed"
  majvii  = [ @,@,@+majCvii2 ]
  majV7   = [ @,@,@+majCV72  ]

"     Correction intervals major scale                       "

INTERVALL
  majCI4    = 95:96
  majCiii11 = 432:437
  majCIV9   = 95:96
  majCV2    = 513:512
  majCV11   = 180:184
  majCvii2  = 513:512
  majCV72   = 513:512


"     Retunings (minor)   (anchor=a !)                       "

UMSTIMMUNG
  mini    = [ @ ]
  minIV   = [ @,@,@,@,@,@,@,@,@,@+minAiv9 ]
  minV    = [ @,@,@,@,@,@,@,@,@,@,@,@+minAV11 ]
  minV7   = [ @ ]

"     Correction intervals minor scale                       "

INTERVALL
  minAiv9  = i80_57 - 6 semi
  minAV11  = i30_19 - i46_57


"     Basic (=melodic) C-major scale                         "

TON
  majC0  = 528    "basic frequency"
  majC1  = majC0 + semi
  majC2  = majC0 + i64_57
  majC3  = majC0 + 3 semi
  majC4  = majC0 + i24_19
  majC5  = majC0 + i4_3
  majC6  = majC0 + 6 semi
  majC7  = majC0 + i3_2
  majC8  = majC0 + 8 semi
  majC9  = majC0 + i32_19
  majC10 = majC0 + 10 semi
  majC11 = majC0 + i23_12
  

"     Basic a-minor scale                                    "

TON
  minA0  = majC0 + i16_19
  minA1  = majC0 - 2 semi
  minA2  = majC0 + i18_19
  minA3  = majC0
  minA4  = majC0 + semi
  minA5  = majC0 + i64_57
  minA6  = majC0 + 3 semi
  minA7  = majC0 + i24_19
  minA8  = majC0 + i4_3
  minA9  = majC0 + 6 semi
  minA10 = majC0 + i3_2
  minA11 = majC0 + i92_57


"     Here comes the (rather crazy) interval section ...     "

INTERVALL
  cent   = 1200 wurzel 2
  semi   = 12 wurzel 2
  octave = 2:1
  i3_2   = 3:2
  i5_4   = 5:4
  i4_3   = 4:3
  i5_3   = 5:3
  i32_19 = 32:19
  i23_12 = 23:12
  i24_19 = 24:19
  i36_19 = 36:19
  i64_57 = 64:57
  i9_8   = 9:8
  i15_8  = 15:8
  i16_19 = 16:19
  i18_19 = 18:19
  i92_57 = 92:57
  i80_57 = 80:57
  i30_19 = 30:19
  i46_57 = 46:57
